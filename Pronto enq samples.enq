
// SO trans list with Aliases
// on index, and dates, explcit joins in where clause
select
	soh.so-order-no
from
//	sales-order-archive  on index 2 = soh
//	sales-order-line-archive  on index 1 = sol
	soh = sales-order-archive  on index 2
	sol = sales-order-line-archive on index 1
where
//	so-user-id-code != "jcaval" and 
//	so-processing-date between  15-mar-2012 and 30-mar-2012 and
	soh.so-territory-code = '601' and
//	so-pos-process-date-time >=  23-apr-2012 and
//	so-pos-process-date-time <= 29-apr-2012
	soh.so-processing-date >= 01-jun-2012 and
	soh.so-processing-date <= 10-jun-2012 and
	soh.so-order-no = sol.so-order-no and
	soh.so-bo-suffix = sol.so-bo-suffix and	
//	sol.sol-line-type = "SS"
	sol.sol-shipped-qty < 0

format is lst
report width is 100



// Prompted Stock Whs Master with SOH
// and with totals
select
	stock-code
	stk-description
	stock-group
	stk-condition-code
	stk-unit-desc
	stk-pack-desc
	stk-pack-qty
	whse-code
	whse-qty-on-hand
	whse-avg-cost
	lf-total-value = whse-qty-on-hand * whse-avg-cost
from
	stock-master
	stock-warehouse-detail
where
	whse-code = input string
		prompt is "Whse Code" and
	whse-qty-on-hand != 0 
format is lst
report width is 200
total on whse-qty-on-hand
total on lf-total-value
title line 1 is "WAC NE 0 with SOH  - is_stwhs5"


// Stock movement list - test of GROUP BY/HAVING
select
	stock-code
	stk-stock-status
	stk-condition-code
	stk-description
	stk-trans-whse
	stk-trans-date
	stk-trans-qty
	ws-stk-total = sum(stk-trans-qty)
	ws-whs-total = max(whse-qty-on-hand)
	ws-diff = max(whse-qty-on-hand) - sum(stk-trans-qty)
	stk-trans-type
	whse-qty-on-hand
from
	stock-master
	stock-warehouse-detail
	stock-movements
where
	stock-code between  "1000000000" and "9999999999" and
	stk-trans-whse = '990' and 
	stk-trans-date > 09-mar-2012 and 
	whse-code = '990' 
group by stock-code
having ws-stk-total > ws-whs-total
format is lst
report width is 150
// suppress stk-trans-whse
suppress stk-trans-type
suppress stk-trans-date
suppress stk-trans-qty
suppress whse-qty-on-hand

// Names and addresses - Outer Join
select
	na-type
	accountcode
	na-name
	na-company
	na-street
from
	deb-master
	outer name-and-address-master
where
	na-type  in  {"DA"," "}
format is lst
report width is 250


// Stock Heirarchy Tree (output = Excel)
select
	stock-master.stock-code
	stock-master.stock-group
	stock-hierarchy-tree.stock-group	
	stock-hierarchy-tree.sht-division
from
	stock-master
	stock-hierarchy-tree
where
	stock-master.stock-group = stock-hierarchy-tree.stock-group
format is excel

// RPT report for selected receipted transactions
select
	po-order-no
	po-backorder-flag
	po-whse-code
	cre-accountcode
	po-received-date
	po-inwards-no
	po-received-qty
	po-received-total-amount
	stock-code
	stk-description
	po-item-price
	po-supplier-unit-description
	sl-location-code
	min(sl-picking-preference)
	sup-stock-code
from
	 purchase-order-line
outer stock-location
	outer purchase-order
	stock-master
	stock-supplier
	

where
	 po-order-no = input
	 numeric
	prompt is "Enter P/O Number"
and	po-backorder-flag = input uppercase 
	string
	prompt is "Enter P/O Suffix"

//	po-order-no = 16471 
//	and po-backorder-flag = " " 
	and po-received-qty != 0 
and sl-location-type in ('20','21','22','30','50','60','61','62','63')
group by 
stock-code

format is rpt
width is 150
field total-po-received-qty like po-received-qty
field total-po-received-total-amount like po-received-total-amount
report header
	page
	set total-po-received-qty = 0
	set total-po-received-total-amount = 0
header
	print "Prompted P/O Goods Receipt  - is_potrans3"  in col 59 
		today () in col 105
 		tod() in col 118
 		"Pageno." in col 128
 		pageno() in col 136
	skip 1
	print
		"Order-No" in col 4
		"Suffix" in col 17
		"Whse" in col 27
		"Supplier" in col 35
		"Recv Date" in col 49
		"Inwards No" in col 64
	skip 1
	print
		po-order-no in col 4
		po-backorder-flag in col 17
		po-whse-code in col 27
		cre-accountcode in col 35
		po-received-date in col 49
		po-inwards-no in col 64
	skip 1
	print
		"Location-code" in col 1
		"Supp Stk Cde" in col 10
		"stock-code" in col 28
		"stk-description" in col 48
		"Item Price" in col 82
		"PO UOM" in col 100
		"Rec Qty" in col 110
		"Recv Amt" in col 125
	skip 1
detail
	print
	sl-location-code in col 1
	sup-stock-code in col 10
		stock-code in col 28
		stk-description in col 48
		po-item-price in col 82
		po-supplier-unit-description in col 100
		po-received-qty in col 110
		po-received-total-amount in col 120
	set total-po-received-qty += po-received-qty
	set total-po-received-total-amount += po-received-total-amount

report footer
	print
		"---------------" in col 110
		"---------------" in col 120
	print
		total-po-received-qty in col 110
		total-po-received-total-amount in col 120
